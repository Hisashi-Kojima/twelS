// Dynamic Earley parserのルール

// ruleの先頭の文字が
//   '_'だったら，そのノードを作らない．
//   '?'だったら，childが1つのとき，そのノードを作らない．
// see here: https://github.com/lark-parser/lark/blob/master/docs/tree_construction.md

// default value of priority is 0.

start: _open mathml _close

?mathml: _open mathml _close _annotation?  // mathやsemantics, mrowなど
       | expr

_annotation: "<annotation" _attr* ">" _CONTENT "</annotation>"

// deal with relational operators
?expr: _open _mspace* expr _mspace* _close  // for mrow
     | sum _more_sum*

_more_sum: relational_operator sum

?sum: (_add | subtract)? product _more_product*
    | _open (_add | subtract)? product _more_product* _close  // for mrow

_more_product: (_add | subtract) product

?product: atom _more_atom*  // a*bのパターン
        | atom+  // abのパターン

_more_atom: (_mul | div) atom

?atom: "<mn" _attr* ">" TOKEN "</mn>"
     | "<mi" _attr* ">" "/" "</mi>" -> slash
     | _mi
     | _mi_empty
     | _mo
     | _mrow_tag product "</mrow>"
     | _left_paren sum _right_paren -> paren
     | func
     | cdots
     | mtext

// this priority is higher than _mo.
cdots.-1: "<mo" _attr* ">" "⋯" "</mo>"

_mi: "<mi" _attr* ">" TOKEN "</mi>"
_mi_empty: "<mi" _attr* "></mi>"

// this priority is lower than atom and cdots.
_mo.-2: "<mo" _attr* ">" TOKEN "</mo>"

?mtext: "<mtext" _attr* ">" TOKEN "</mtext>"

// some mspace tags are used for a math expression delimiter.
// some other mspace tags are used for just spaces. <- not a delimiter.
_mspace: "<mspace" _attr* "/>"

// tableのruleのときに，funcのノードを残したくなかったので，funcの先頭に'?'を付けた
?func: "<msqrt" _attr* ">" expr "</msqrt>" -> sqrt
     | "<mfrac" _attr* ">" expr expr "</mfrac>" -> frac
     | "<msup" _attr* ">" expr expr "</msup>" -> sup
     | _sub_tag expr expr "</msub>" -> sub
     | _subsup_tag expr expr expr "</msubsup>" -> subsup
     | "<mroot" _attr* ">" expr expr "</mroot>" -> root
     // use expr for dealing with relational operators
     | "<mover" _attr* ">" expr expr "</mover>" -> over
     | under
     | summation
     | product_of_seq
     | integral
     | lim
     | underover
     | table

// for mtable
table: "<mtable" _attr* ">" mtr+ "</mtable>"
mtr: "<mtr" _attr* ">" mtd+ "</mtr>" -> tr
mtd: "<mtd" _attr* ">" mathml "</mtd>" -> td

// underとsub、underoverとsubsupは意味が異なると思い正規化をしない。
// limやsummation、product_of_seqの中ではこれらを正規化する。
under: _under_tag expr expr "</munder>"
lim: _under_tag _lim_op_1 _mrow_tag atom _lim_op_2 atom "</mrow></munder>" expr
   | _sub_tag _lim_op_1 _mrow_tag atom _lim_op_2 atom "</mrow></msub>" expr

underover: _underover_tag expr expr expr "</munderover>"
summation: _underover_tag _summation_op expr expr "</munderover>" expr
         | _subsup_tag _summation_op expr expr "</msubsup>" expr
product_of_seq: _underover_tag _product_of_seq_op expr expr "</munderover>" expr
              | _subsup_tag _product_of_seq_op expr expr "</msubsup>" expr
integral: _underover_tag _integral_op_1 expr expr "</munderover>" expr _integral_op_2 atom
        | _subsup_tag _integral_op_1 expr expr "</msubsup>" expr _integral_op_2 atom

_open: "<" _ELEMENTS _attr* ">"
_close: "</" _ELEMENTS ">"

_add: _open "+" _close
subtract: _open ("−" | "-") _close
_mul: _open ("*" | "×" | "⋅") _close
div: _open ("/" | "÷") _close
equal: _open "=" _close
less: _open ("<" | "&lt;") _close
greater: _open (">" | "&gt;") _close
in: _open "∈" _close
_left_paren: _open "(" _close
_right_paren: _open ")" _close

_mrow_tag: "<mrow" _attr* ">"
_under_tag: "<munder" _attr* ">"
_underover_tag: "<munderover" _attr* ">"
_sub_tag: "<msub" _attr* ">"
_subsup_tag: "<msubsup" _attr* ">"

_lim_op_1: "<mo" _attr* ">lim</mo>"
_lim_op_2: "<mo" _attr* ">" ("&#x2192;" | "→") "</mo>"
_summation_op: "<mo" _attr* ">" ("&#x2211;" | "∑") "</mo>"
_product_of_seq_op:  "<mo" _attr* ">" ("&#x220F;" | "∏") "</mo>"
_integral_op_1: "<mo" _attr* ">" ("&#x222B;" | "∫") "</mo>"
_integral_op_2: ("<mi>d</mi>" | "<mrow><mi>d</mi></mrow>")

?relational_operator: equal
                    | less
                    | greater
                    | in

_attr: " "* _ATTRIBUTE

TOKEN: /[^<> ]+/
_CONTENT: /[^<>]+/
_ATTRIBUTE: /[^<>= ]+=("[^"]*"|'[^']*')/  // シングルクォーテーションとダブルクォーテーションに対応．
COMMENT: /<!--[^(-->)]*-->/

_ELEMENTS: "math"
         | "maction"
         | "maligngroup"
         | "malignmark"
         | "men_close"
         | "merror"
         | "mfenced"
         | "mglyph"
         | "mlabeledtr"
         | "mlongdiv"
         | "mmultiscripts"
         | "mo"
         | "mpadded"
         | "mphantom"
         | "mrow"
         | "mscarries"
         | "mscarry"
         | "msgroup"
         | "msline"
         | "msrow"
         | "mstack"
         | "mstyle"
         | "ms"  // msはmstyleなどよりも後に記述しなければ，mstyleなどが無視されてしまう．
         | "semantics"
         | "annotation"
         | "annotation-xml"

%import common.WS -> _WS  // white space
%ignore _WS
%ignore COMMENT